apply plugin: 'com.android.library'

// 仅在存在 google-services.json 且满足条件时应用 Google Services 插件
def hasGoogleServicesJson = file('google-services.json').exists()
if (hasGoogleServicesJson && (project.hasProperty('enableAnalytics') || gradle.startParameter.taskNames.any { it.contains('Release') })) {
    apply plugin: 'com.google.gms.google-services'
} else if (!hasGoogleServicesJson) {
    logger.lifecycle("google-services.json not found; skipping Google Services plugin")
}

android {
    ndkVersion "27.0.12077973"

    compileSdkVersion 36

    namespace 'com.limelight'

    defaultConfig {
        minSdk 22
        targetSdk 35

        versionName "12.6.0"
        versionCode = 357

        // Generate native debug symbols to allow Google Play to symbolicate our native crashes
        ndk.debugSymbolLevel = 'FULL'
    }

    flavorDimensions.add("root")

    buildFeatures {
        buildConfig = true
    }

    productFlavors {
        root {
            // Android O has native mouse capture, so don't show the rooted
            // version to devices running O on the Play Store.
            maxSdk 25

            externalNativeBuild {
                ndkBuild {
                    arguments "PRODUCT_FLAVOR=root"
                }
            }

        // applicationId "com.limelight.root" // Removed for library conversion
            dimension "root"
            buildConfigField "boolean", "ROOT_BUILD", "true"
        }

        nonRoot {
            externalNativeBuild {
                ndkBuild {
                    arguments "PRODUCT_FLAVOR=nonRoot"
                }
            }

        // applicationId "com.limelight" // Removed for library conversion
            dimension "root"
            buildConfigField "boolean", "ROOT_BUILD", "false"
        }
    }

    compileOptions {
        encoding "UTF-8"
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    lint {
        disable 'MissingTranslation'
        lintConfig file('lint.xml')
    }

    // bundle {
    //    language {
    //        enableSplit = false
    //    }
    //    density {
    //        enableSplit = false
    //    }
    // }

    // 基于环境变量的可选签名配置（用于 CI 等环境）
    def keystorePath = System.getenv("KEYSTORE_PATH")
    def keystorePassword = System.getenv("KEYSTORE_PASSWORD")
    def envKeyAlias = System.getenv("KEY_ALIAS")
    def envKeyPassword = System.getenv("KEY_PASSWORD")
    def hasSigningConfig = keystorePath != null && keystorePassword != null && envKeyAlias != null && envKeyPassword != null && new File(keystorePath).exists()

    signingConfigs {
        if (hasSigningConfig) {
            release {
                storeFile file(keystorePath)
                storePassword keystorePassword
                keyAlias envKeyAlias
                keyPassword envKeyPassword
            }
        }
    }

    buildTypes {
        debug {
            // applicationIdSuffix ".vplus_debug" // Library 不能有 applicationIdSuffix
            resValue "string", "app_label", "月光-威力加强版"
            resValue "string", "app_label_root", "月光-威力加强版"

            minifyEnabled false // Library 通常不建议开启混淆，除非有特殊需求，且需要 consumerProguardFiles
            // proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            // applicationIdSuffix ".qiin" // Library 不能有 applicationIdSuffix
            resValue "string", "app_label", "Moonlight"
            resValue "string", "app_label_root", "Moonlight (Root)"

            minifyEnabled false // Library 通常不建议开启混淆
            // proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            // if (hasSigningConfig && signingConfigs.hasProperty('release')) {
            //    signingConfig signingConfigs.release
            // }
        }
    }

    externalNativeBuild {
        ndkBuild {
            path "src/main/jni/Android.mk"
        }
    }
}

dependencies {
    implementation 'org.bouncycastle:bcprov-jdk18on:1.82'
    implementation 'org.bouncycastle:bcpkix-jdk18on:1.82'
    implementation 'org.jcodec:jcodec:0.2.5'
    implementation 'com.squareup.okhttp3:okhttp:5.2.1'
    implementation 'org.jmdns:jmdns:3.6.2'
    implementation 'com.github.cgutman:ShieldControllerExtensions:1.0.1'
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'com.github.ZeyuKeithFu:KeyboardHeaderLayout:v1.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    implementation 'com.google.code.gson:gson:2.13.2'
    implementation 'com.github.bumptech.glide:glide:5.0.5'
    implementation 'jp.wasabeef:glide-transformations:4.3.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.4.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:5.0.5'
    implementation 'com.squareup:seismic:1.0.3'
    implementation 'com.google.android.flexbox:flexbox:3.0.0'

    // Firebase Analytics
    implementation platform('com.google.firebase:firebase-bom:32.7.0')
    implementation 'com.google.firebase:firebase-analytics'
}
